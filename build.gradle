plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'com.epages.restdocs-api-spec' version '0.19.2'
    id 'nu.studer.jooq' version '9.0'
}

group = 'wonjun'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    // jOOQ 관련 의존성
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    jooqGenerator 'com.mysql:mysql-connector-j'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // OAuth2 Client
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:mysql:1.19.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // AsciiDoctor extension for REST Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Flyway for DB migration
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
}

// jOOQ 코드 생성 설정 - Local 환경용
jooq {
    version = dependencyManagement.importedProperties['jooq.version']

    configurations {
        main {
            generationTool {
                logging = 'INFO'

                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3306/stidb'
                    user = 'root'
                    password = 'qwe123'
                }

                generator {
                    name = 'org.jooq.codegen.JavaGenerator'

                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'stidb'

                        // 생성할 테이블 패턴 (필요시 조정)
                        includes = '.*'
                        excludes = 'flyway_schema_history'
                    }

                    generate {
                        deprecated = false
                        records = true
                        pojos = true
                        daos = true
                        fluentSetters = true
                    }

                    target {
                        packageName = 'wonjun.jooq.generated'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}

// 1. 테스트 설정
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

// 2. OpenAPI 스펙 생성 설정
openapi3 {
    server = 'https://stiky-api-mh6lf6mzkq-du.a.run.app'
    title = 'Stiky API'
    description = 'Stiky API 명세서'
    version = '1.0.0'
    format = 'yaml'
    outputDirectory = 'build/api-spec'
}

// 3. AsciiDoctor 설정
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test

    baseDirFollowsSourceFile()
}

// 4. 문서 복사 태스크
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

task copyOasToSwagger(type: Copy) {
    dependsOn 'openapi3'
    from "build/api-spec/openapi3.yaml"
    into "src/main/resources/static/docs"
}

afterEvaluate {
    tasks.named('openapi3') {
        dependsOn 'test'
    }
}

// 5. 빌드 및 패키징
build {
    dependsOn copyDocument
    dependsOn copyOasToSwagger
}

bootJar {
    dependsOn copyDocument
    dependsOn copyOasToSwagger

    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
    from("src/main/resources/static/docs") {
        include 'openapi3.yaml'
        into 'static/docs'
    }
}

// jOOQ 생성은 수동으로 실행
// ./gradlew generateJooq