plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'com.epages.restdocs-api-spec' version '0.19.2'
}

group = 'wonjun'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:mysql:1.19.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // AsciiDoctor extension for REST Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// OpenAPI 스펙 생성 설정
openapi3 {
    server = 'http://localhost:8080'
    title = 'API 문서'
    description = 'Spring Boot REST API 문서'
    version = '1.0.0'
    format = 'yaml'
    outputDirectory = 'src/main/resources/static/docs'
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test

    baseDirFollowsSourceFile()

    doFirst {
        delete file('src/main/resources/static/docs')
    }
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

task copyOasToSwagger(type: Copy) {
    dependsOn 'openapi3'
    from "src/main/resources/static/docs/openapi3.yaml"
    into "src/main/resources/static/"

    doFirst {
        def sourceFile = file("src/main/resources/static/docs/openapi3.yaml")
        if (!sourceFile.exists()) {
            logger.warn("openapi3.yaml not found at ${sourceFile.absolutePath}")
        }
    }
}

build {
    dependsOn copyDocument
    finalizedBy copyOasToSwagger
}

bootJar {
    dependsOn copyDocument
    dependsOn 'openapi3'

    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
    from("src/main/resources/static/docs") {
        include 'openapi3.yaml'
        into 'static'
    }
}